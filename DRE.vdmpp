class DRE
instance variables
	private code: nat1 :=9999;
	private memory: map Candidate to nat := {|->} ;
	private log: seq of RTAL := [];
	private CurrentPBE: PBE;
	public StateMachine: VoteState := new VoteState();
	private CurrentChoice: Candidate := new Candidate(); 
	private CurrentVoter: Voter := new Voter();
operations
-- Constructor
	public DRE: PBE ==> DRE 
	DRE(pbe) == (
							dcl logEntry: RTAL;
							CurrentPBE := pbe;
							StateMachine.toStop();
						 	logEntry := new RTAL("Election Started");
							log := log ^ [logEntry];
							for all cand in set CurrentPBE.ballot do
				       (
				       	memory:=memory++{cand|->0};
				       );
							return self)
	pre 
		pbe.ballot <> {}
		and pbe.voters <> {}
		and pbe.code = code
	post 
		self.CurrentPBE.ballot = pbe.ballot
		and self.CurrentPBE.voters = pbe.voters;
	
	public getResult:() ==> map Candidate to nat
	getResult()== return memory;
	
--Voting sequence state machine
	public StartVote: Voter ==> ()
	StartVote(v) == (
			dcl logEntry: RTAL;
			logEntry := new RTAL("Started voting: " ^ v.name);
			log := log ^ [logEntry];
			StateMachine.toInit();
			CurrentVoter := v;
	)
	pre StateMachine.currentState=<STOP>
			and card CurrentPBE.voters <> 0
			and v in set CurrentPBE.voters
			and v not in set CurrentPBE.previousVoters
	post StateMachine.currentState=<INIT>
			 and v = CurrentVoter;

	public Vote: Candidate ==> ()
	Vote(c) == (
			dcl logEntry: RTAL;
			logEntry := new RTAL(CurrentVoter.name ^ " has voted");
			log := log ^ [logEntry];
			StateMachine.toConfirm();
			CurrentChoice := c
	)
	pre StateMachine.currentState=<INIT>
			and CurrentVoter in set CurrentPBE.voters
			and c in set CurrentPBE.ballot
	post StateMachine.currentState=<CONFIRM>
			 and c = CurrentChoice
			 and CurrentVoter in set CurrentPBE.voters
			 and CurrentChoice in set CurrentPBE.ballot;
	
	public ConfirmVote: bool ==>()
	ConfirmVote(confirm) == (
			if(confirm)
			then(
					dcl logEntry: RTAL;
					dcl currentCount: nat;
					logEntry := new RTAL("Confirmed: " ^ CurrentVoter.name ^ " has voted");
					log := log ^ [logEntry];
					currentCount:=memory(CurrentChoice)+1;
					memory:= memory ++ {CurrentChoice|->currentCount};
					CurrentPBE.voteAct(CurrentVoter);	
					if(card CurrentPBE.voters = 0)
					then(
						StateMachine.toEnd();
					)
					else(
						StateMachine.toStop();
					)
					)
			else(
					dcl logEntry: RTAL;
					logEntry := new RTAL("Canceled: " ^ CurrentVoter.name ^ " has canceled the vote");
					log := log ^ [logEntry];
					StateMachine.toInit();
					)
	)
	pre StateMachine.currentState=<CONFIRM> 
			and CurrentVoter.name <> []
			and CurrentChoice.name <> []
	post (StateMachine.currentState=<STOP>
			 and CurrentVoter in set CurrentPBE.previousVoters
			 and CurrentVoter not in set CurrentPBE.voters
			 )
			 or (
			 StateMachine.currentState=<INIT>
			 and CurrentVoter in set CurrentPBE.voters)
			 or (
			 StateMachine.currentState=<END>
			 and CurrentVoter in set CurrentPBE.previousVoters
			 and CurrentVoter not in set CurrentPBE.voters
			 );
end DRE